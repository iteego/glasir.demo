buildscript {
  ext.SettingsRepositoryConfigurator = {
    maven {
      credentials.username artifactoryReader
      credentials.password artifactoryReaderPwd
      url "$artifactoryReaderUrl/glasir"
    }
    mavenCentral()
  }

  ext.SettingsDependencyConfigurator = {
    classpath "com.iteego.glasir:glasir.build:1.3.16-SNAPSHOT"
  }

  ext.SettingsResolutionConfigurator = {
    resolutionStrategy.cacheChangingModulesFor 1, 'minutes'
  }

  repositories             SettingsRepositoryConfigurator
  dependencies             SettingsDependencyConfigurator
  configurations.classpath SettingsResolutionConfigurator
}

apply plugin: "glasir.settings"

glasir.settings { 
  rootProjectName "glasir.demo"
  cachePackagesFor 24, "hours"

  dependencies {
    glasirpackage "com.iteego.glasir.pkg:atg-10.0.3:+"
    glasirpackage "com.iteego.glasir.pkg:atg-initializer-10.0.3:+"
    glasirpackage "com.iteego.glasir.pkg:jboss-eap-5.1.0:1.1.2"
    glasirpackage "com.iteego.glasir.pkg:glasir.db:1.1.4-SNAPSHOT"
  }
  
  addAtgModules from: atgRoot
  addAtgModules from: "$settingsDir/packages/modules"
  addAtgModules from: "$settingsDir/packages/initializer"
  addAtgModules from: "$settingsDir/glasir-app", isAtgCore: false
}

gradle.rootProject { 
  ext { 
    RepositoryConfigurator = SettingsRepositoryConfigurator
    DependencyConfigurator = SettingsDependencyConfigurator
    ResolutionConfigurator = SettingsResolutionConfigurator
  }
}

gradle.allprojects {
  repositories SettingsRepositoryConfigurator
}



