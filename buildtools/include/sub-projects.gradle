/******************************************************************************
 * Gradle project and sub-project setup calls
 *****************************************************************************/

//add bogus tasks to the root project to enable child-project builds via 
//'gradle build' in the root project
['clean','build','test'].each {
  task "$it" {}
}


//the below is a list of test drivers for the geb web testing framework.
//a driver can emulate a specific browser and excercise the application UI.
//the htmlunit driver is a headless driver suitable for CI testing of the application UI
//See http://www.gebish.org/manual/current/configuration.html#driver_implementation
//for more details
//available drivers: htmlunit, firefox, ie, chrome, android                
drivers = ['htmlunit']

allprojects {  
  project.isBuildProject = new File(projectDir, "/src").isDirectory()
  if (!project.isBuildProject) {    
    return
  }
  
  apply plugin: 'glasir.build'
  apply plugin: 'groovy'

  dependencies {
    groovy "org.codehaus.groovy:groovy:1.7.10"

    testCompile "junit:junit:4.8.2", 
                "org.spockframework:spock-core:0.5-groovy-1.7",
                "org.codehaus.geb:geb-spock:0.6.1",
                "org.codehaus.geb:geb-junit4:0.6.1"

    drivers.each { driver ->
      testCompile "org.seleniumhq.selenium:selenium-${driver}-driver:2.9.0"
    }

    testRuntime "org.seleniumhq.selenium:selenium-support:2.9.0"
  }
  
  sourceCompatibility = "1.6"
  targetCompatibility = "1.6"
  
  compileJava {
    options.compilerArgs = ['-Xlint:deprecation'] //, '-Xlint:unchecked']
  }

  drivers.each { driver -> 
    task "${driver}Test"(type: Test) {
      isWebTest = true
      include '**/*GebSpec.class'
  
      testReportDir = file("${reporting.baseDir}/geb-tests")
      testResultsDir = file("$buildDir/geb-test-results/")
  
      systemProperty "geb.build.reportsDir", "${reporting.baseDir}/$name/geb"
      systemProperty "geb.env", driver
      
      //TODO: Externalize the below host/port configuration somehow
      systemProperty "geb.build.baseUrl", "http://localhost:10080"
    }  
  }

  jar  {
    //only create a jar file if we have some classes to jar up, 
    //to to this we check if the 'compileJava' task did any work (if so, we have something
    //to jar up). TODO: if we need to create resources-only jars, this needs changing
    onlyIf {
      compileJava.didWork || processResources.didWork || compileGroovy.didWork
    }

    // Change where the resulting jar will end up
    destinationDir = file("lib")
    baseName = 'classes'
  }

  test {
    exclude '**/*GebSpec.class'
  }

  clean << {
    delete file: 'lib/classes.jar'
    if (!file('lib').listFiles()) {
      delete dir: 'lib'
    }
  }  
}

subprojects {
  if (!project.isBuildProject) return

  group = glasir.projectGroup

  //configures dependencies for this project based on the atg MANIFEST.MF file
  //ATG-Required attribute values. Applies these dependencies to the configurations
  //given as the second argument
  glasir {
    atgDependencies {
      applyToConfigurations "compile", "testCompile"      
      verbose false
    }     
  }
}

task aggregateTestReports(type: TestReportAggregator) {
  def buildDir = file("$projectDir/build")
  def reportsDir = file("$buildDir/reports")
  reportsDir.mkdirs()
  
  dependsOn getTasksByName('check', true)

  includeWebTests = false
  testReportDir = file("${reportsDir}/tests")
  testResultsDir = file("${buildDir}/test-results")
  projects = subprojects
  doLast {
    println "> "
    println "> Aggregated test report saved at $testReportDir/index.html"
    println "> "
  }    
}
build.dependsOn(aggregateTestReports)

gradle.projectsEvaluated { g ->
  build.dependsOn(getTasksByName('build', true) - build)
  clean.dependsOn(getTasksByName('clean', true) - clean)
}

class TestReportAggregator extends Copy {
  def projects
  File testResultsDir
  boolean includeWebTests = false
  
  @OutputDirectory
  File testReportDir
  
  def TestReportAggregator() {
    dependsOn { testTasks }
    from { inputTestResultDirs }
    into { testResultsDir }
  }

  @TaskAction
  def aggregate() {
    def report = new org.gradle.api.internal.tasks.testing.junit.report.DefaultTestReport(testReportDir: testReportDir, testResultsDir: testResultsDir)
    report.generateReport()
  }

  def getTestTasks() {
    def testTasks = projects.collect { it.tasks.withType(Test) }.flatten()
    
    //exclude web tests from the 
    includeWebTests ? testTasks : testTasks.findAll { !it.hasProperty('isWebTest') } 
  }

  def getInputTestResultDirs() {
    testTasks*.testResultsDir
  }
}
